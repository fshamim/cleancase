apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 2
        versionName "2.0"
        buildConfigField "boolean", "BLUETOOTH_ON", 'true'
        buildConfigField "boolean", "WIFI_ON", 'true'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.+'
    compile 'com.github.fshamim:ibus:0.0.1'
    compile 'com.github.fshamim:cleanlog:0.0.1'
    compile 'com.github.fshamim:injectables:0.0.1'

    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    provided "org.projectlombok:lombok:1.12.6"
    apt "org.projectlombok:lombok:1.12.6"

    compile 'javax.annotation:jsr250-api:1.0'

    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.github.johnpersano:supertoasts:1.3.4@aar'
    compile 'io.reactivex:rxandroid:0.24.0'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}